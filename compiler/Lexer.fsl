{
open System
open FSharp.Text.Lexing
open Compiler.Helpers
open Compiler.Helpers.Error.Lexer
open Compiler.Base
open Parser

let lexeme = LexBuffer<_>.LexemeString

let private newline (lexbuf: LexBuffer<_>) =
  lexbuf.EndPos <- lexbuf.EndPos.NextLine

let fixStrEscapes s = 
  let strReplace (old:string) (newVal:string) (s:string) = s.Replace (old, newVal)
  s
  |> strReplace "\\n" "\n" 
  |> strReplace "\\t" "\t"
  |> strReplace "\\r" "\r"
  |> strReplace "\\0" "\x00"
  |> strReplace "\\\\" "\\"
  |> strReplace "\\'" "'"
  |> strReplace "\\\"" "\""
}

let letter = ['a' - 'z']
let digit = ['0' - '9']
let operator = ['(' ')' '+' '-' '/' '*' '%' '=']
let whitespace = [' ' '\t']
let newline = '\r' | '\n' | "\r\n"

let integer = digit*

let fractional = '.' digit+
let exp = ['e' 'E'] ['+' '-']? digit+
let real = integer fractional? exp?

let identifier = ['a'-'z' 'A'-'Z'] ['a'-'z' 'A'-'Z' '0'-'9' '_']*

let escapes = ['n' 't' 'r' '0' '\\' '\'' '"']


rule read =
  parse
  | "integer"     { K_basicType (Integer) }
  | "boolean"     { K_basicType (Boolean) }
  | "char"        { K_basicType (Character) }
  | "real"        { K_basicType (Real) }

  | "array"       { K_array }
  | "of"          { K_of }

  | "program"     { K_program }
  | "begin"       { K_begin }
  | "end"         { K_end }
  | "procedure"   { K_procedure }
  | "function"    { K_function }
  | "forward"     { K_forward }
  | "label"       { K_label }
  | "result"      { K_result }

  | "if"          { K_if }
  | "then"        { K_then }
  | "else"        { K_else }
  | "while"       { K_while }
  | "do"          { K_do }
  | "goto"        { K_goto }
  | "return"      { K_return }
  | "new"         { K_new }
  | "dispose"     { K_dispose }

  | "true"        { K_true }
  | "false"       { K_false }
  | "nil"         { K_nil }

  | "var"         { K_var }

  | ":="          { O_assign }
  | "@"           { O_addr }
  | "not"         { O_not }
  | "+"           { O_plus }
  | "-"           { O_minus }
  | "*"           { O_mult }
  | "/"           { O_div }
  | "div"         { O_divi }
  | "mod"         { O_mod }
  | "or"          { O_or }
  | "and"         { O_and }
  | "="           { O_equals }
  | "<>"          { O_nequals }
  | "<"           { O_less }
  | "<="          { O_leq }
  | ">"           { O_greater }
  | ">="          { O_geq }

  | ";"           { Semicolon }
  | ":"           { Colon }
  | ","           { Comma }
  | "["           { Lbrack }
  | "]"           { Rbrack }
  | "^"           { Carret }
  | "."           { Dot }
  | "("           { Lparen }
  | ")"           { Rparen }

  | identifier    { T_id (lexeme lexbuf) }
  | integer       { T_int (int <| lexeme lexbuf) }
  | real          { T_real (lexeme lexbuf) }
  | '"'           { T_str (fixStrEscapes <| readString "" lexbuf + "\x00") }
  | '\''          { T_char (readChar "" lexbuf) }
  | "(*"          { consumeComment lexbuf |> ignore; read lexbuf }
  | newline       { newline lexbuf; read lexbuf }
  | whitespace+   { read lexbuf }
  | eof           { EOF }
  | _             { RaiseLexError "Unexpected character" lexbuf }

and readString str =
  parse
  | '"'                   { str }
  | '\\'                  { readString (str + readEscape "\\" lexbuf) lexbuf }
  | [^'"' '\\' '\n']+     { readString (str + (lexeme lexbuf)) lexbuf }
  | eof | '\n'            { RaiseLexError "String stream not terminated" lexbuf }

and readChar str =
  parse
  | '\''                  { if CheckCharacter str = false then RaiseLexError "Expected single character but found multi-character stream" lexbuf else str }
  | '\\'                  { readChar (str + readEscape "\\" lexbuf) lexbuf }
  | eof | '\n'            { RaiseLexError "Character stream not terminated" lexbuf }
  | _                     { readChar (str + lexeme lexbuf) lexbuf }

and readEscape str =
  parse
  | escapes     { str + lexeme lexbuf }
  | _           { RaiseLexError "Bad escape sequence" lexbuf }

and consumeComment =
  parse
  | "*)"        { }
  | "\n"        { newline lexbuf; consumeComment lexbuf }
  | _           { consumeComment lexbuf }
  | eof         { RaiseLexError "Comment stream not terminated" lexbuf }